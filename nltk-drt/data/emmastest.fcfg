% start S
############################
# Grammar Rules
#############################

S[SEM = <app(?subj,?vp)>] -> NP[NUM=?n,SEM=?subj] VP[NUM=?n,SEM=?vp]

NP[NUM=?n,SEM=<?det(?nom)> ] -> Det[NUM=?n,SEM=?det]  Nom[NUM=?n,SEM=?nom]
Det[SEM=<?possmark(?np)>] -> NP[SEM=?np] PosMarker[SEM=?possmark]
NP[LOC=?l,NUM=?n,SEM=?np] -> PropN[LOC=?l,NUM=?n,SEM=?np]
NP[LOC=?l,NUM=?n,SEM=?np] -> Pron[LOC=?l,NUM=?n,SEM=?np]

Nom[NUM=?n,SEM=?nom] -> N[NUM=?n,SEM=?nom]
Nom[NUM=?n,SEM=<app(?pp,?nom)>] -> N[NUM=?n,SEM=?nom] PP[SEM=?pp]

VP[NUM=?n,SEM=?v] -> IV[NUM=?n,SEM=?v]
VP[NUM=?n,SEM=<app(?v,?obj)>] -> TV[NUM=?n,SEM=?v] NP[SEM=?obj]

#############################
# Lexical Rules
#############################

Det[NUM=sg,SEM=<\P Q.DRS([],[((DRS([x],[])+P(x)) implies Q(x))])>] -> 'every' | 'Every' 
Det[NUM=pl,SEM=<\P Q.DRS([],[((DRS([x],[])+P(x)) implies Q(x))])>] -> 'all' | 'All'
Det[SEM=<\P Q.((DRS([x],[])+P(x))+Q(x))>] -> 'some' | 'Some'
Det[NUM=sg,SEM=<\P Q.((DRS([x],[])+P(x))+Q(x))>] -> 'a' | 'A'
Det[NUM=sg,SEM=<\P Q.(DRS([],[not((DRS([x],[])+P(x))+Q(x))]))>] -> 'no' 


# Proper name
PropN[NUM=sg,SEM=<\P.(DRS([],[PROP([x],[John(x)])])+P(x))>] -> 'John'
PropN[NUM=sg,SEM=<\P.(DRS([],[PROP([x],[Mia(x)])])+P(x))>] -> 'Mia'
# Pronoun
Pron[NUM=sg, GEN=m, SEM=<\P.(DRS([],[PRON([x],[])])+P(x))>] -> 'he' | 'He'
Pron[NUM=sg, GEN=f, SEM=<\P.(DRS([],[PRON([x],[])])+P(x))>] -> 'she' | 'She'
# Definite article
Det[NUM=sg,SEM=<\P Q.(DRS([],[(DEF([x],[])+P(x))])+Q(x))>] -> 'the' | 'The'
# Possessive pronoun
Det[NUM=sg,GEN=m,SEM=<\P Q.(DRS([],[(DEF([x],[poss(y,x), PRON([y],[])])+P(x))])+Q(x))>] -> 'his' | 'His'
Det[NUM=sg,GEN=f,SEM=<\P Q.(DRS([],[(DEF([x],[poss(y,x), PRON([y],[])])+P(x))])+Q(x))>] -> 'her' | 'Her'
# Reflexive pronoun
Pron[NUM=sg, GEN=m, SEM=<\P.(DRS([],[PRON([x],[])])+P(x))>] -> 'himself'
Pron[NUM=sg, GEN=f, SEM=<\P.(DRS([],[PRON([x],[])])+P(x))>] -> 'herself'
# Possessive marker 's
PosMarker[SEM=<\O P Q.(DRS([],[(O(\x.(DEF([y],[poss(x,y)])))+P(y))])+Q(y))>] -> 's'


N[NUM=sg,SEM=<\x.DRS([],[boy(x)])>] -> 'boy' 
N[NUM=sg,SEM=<\x.DRS([],[girl(x)])>] -> 'girl'
N[NUM=sg,SEM=<\x.DRS([],[father(x)])>] -> 'father'


IV[NUM=sg,SEM=<\x.DRS([],[walk(x)])>,tns=pres] -> 'walks'

TV[NUM=sg,SEM=<\X x.X(\y.DRS([],[marry(x,y)]))>,tns=pres] -> 'marries'